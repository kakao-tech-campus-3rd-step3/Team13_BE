plugins {
    alias libs.plugins.java
    alias libs.plugins.spotless
    alias libs.plugins.spring.boot
    alias libs.plugins.spring.dependency.management
}

group = 'com.b4f2'
version = '0.0.1-SNAPSHOT'
description = '종합 스포츠 매칭 플랫폼 pting'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

/*
 * ./gradlew spotlessInstallGitPrePushHook
 * ./gradlew.bat spotlessInstallGitPrePushHook
 */
spotless {
    java {
        forbidWildcardImports()
        removeUnusedImports()
        palantirJavaFormat('2.81.0')
        formatAnnotations()
        importOrder(
            '\\#',
            'java',
            'javax',
            'jakarta',
            'org',
            'com',
            '',
            'com.b4f2'
        )
        trimTrailingWhitespace()
        endWithNewline()
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring Boot
    implementation libs.spring.boot.starter.web
    implementation libs.spring.boot.starter.data.jpa
    implementation libs.spring.boot.starter.validation

    // Test
    testImplementation libs.spring.boot.starter.test
    testRuntimeOnly libs.junit.platform.launcher

    // Lombok
    annotationProcessor libs.lombok
    compileOnly libs.lombok

    // Database
    implementation libs.postgresql

    // TestContainers
    testImplementation libs.spring.boot.testcontainers
    testImplementation libs.testcontainers.junit.jupiter
    testImplementation libs.testcontainers.postgresql

    // JWT
    runtimeOnly libs.jjwt.impl
    runtimeOnly libs.jjwt.jackson
    implementation libs.jjwt.api

    // Firebase Admin for FCM
    implementation libs.firebase.admin

    // Spring Mail
    implementation libs.spring.boot.starter.mail

    // Open Skill for MMR
    implementation libs.openSkill

    // Spring Cloud AWS for S3
    implementation libs.spring.cloud.starter.aws
}

tasks.named('test') {
    useJUnitPlatform {
        if (System.getProperty('ci') != null) {
            excludeTags('integration')
        }
    }
}
